alter procedure cocoa_query_analyzer.database_objects
as

begin try
	drop table #tmpCQAobjects
end try
begin catch
end catch 

select 	
	schemas.name schema_name,
	objects.name object_name,
	type,
	--type_desc,
	create_date,
	modify_date,
	object_id,
	objects.schema_id,
	case
		when type = 'P' then 'procedures'
		when type = 'U' then 'tables' 	
		when type = 'V' then 'views'
		when type in ('TF', 'IF', 'FN') then 'functions' 
		else type 
	end object_type,
	case
		when type = 'P' then 1
		when type = 'U' then 0 	
		when type = 'V' then 2
		when type in ('TF', 'IF', 'FN') then 3
		else 4 
	end object_type_order 
into #tmpCQAobjects
from sys.objects 
	inner join sys.schemas on schemas.schema_id = objects.schema_id
where 
	type in ('U', 'V', 'P', 'TF', 'IF', 'FN') 
	and not (type = 'P'	and objects.name like 'dt_%') --hide ms source safe procedures
order by 
	schema_name,
	type,
	objects.name

declare @results table(id varchar(255) primary key,
	parent_id varchar(255),
	display_name varchar(255),
	objects_count int,
	modify_date datetime,
	[level] int,
	[order] int,
	full_name varchar(255),
	type varchar(2))
	
insert into @results
select 
	'schema:' + cast(schema_id as varchar(50)) id,
	'' parent_id,
	schema_name display_name,
	count(*) objects_count,
	max(modify_date) modify_date,
	0,
	null,
	null,
	null
from 
	#tmpCQAobjects
group by
	schema_id, schema_name
order by
schema_name

insert into @results
select 
	object_type + ':' + cast(schema_id as varchar(50)) id,
	'schema:' + cast(schema_id as varchar(50)) parent_id,
	object_type display_name,
	count(*) objects_count,
	max(modify_date) modify_date,
	1,
	object_type_order,
	null,
	null
from 
	#tmpCQAobjects
group by
	schema_id, schema_name, object_type_order, object_type
order by
	schema_name, object_type_order, object_type
insert into @results
select 
	'object:' + cast(object_id as varchar(50)) id,
	object_type + ':' + cast(schema_id as varchar(50)) parent_id,	
	object_name display_name,
	0 objects_count,
	modify_date,
	2,
	null,
	'[' + schema_name + '].[' + object_name + ']',
	type
from #tmpCQAobjects 
order by 
	schema_name, object_type_order, object_name	


select id, parent_id, display_name, objects_count, modify_date, full_name, type
from 
	@results 
order by 
	[level], [order], display_name
	
drop table #tmpCQAobjects


